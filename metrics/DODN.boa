# Depth of Declaration Nesting
p: Project = input; 

# output pattern: "metric[projectID][className/classIndex] = metricValue
m: output collection[string][string] of string;

SEP := ", ";

declId : int = 0;
classIndex : int = 0;

declarationStack : stack of string;
map_DeclId_ClassId : map[string] of string;
map_DeclId_Depth : map[string] of string;

visit(p, visitor {
	before node: Project -> {
		ifall (i: int; !match(`^java$`, lowercase(node.programming_languages[i]))) stop; 
		if(len(node.code_repositories) > 0) visit(node.code_repositories[0]); 
		stop;
	}
	before node: CodeRepository -> {
		if(len(node.revisions) < 100) stop;
		snapshot := getsnapshot(node);
		foreach (i: int; def(snapshot[i]))
			visit(snapshot[i]);
		stop;
	}
	after node : ChangedFile -> {
		declarationsId := keys(map_DeclId_ClassId);
		foreach(i : int; def(declarationsId[i])) {
			classId := map_DeclId_ClassId[declarationsId[i]];
			declarationsDepth := map_DeclId_Depth[declarationsId[i]];				
			m[p.id][classId] << declarationsDepth;
		}
		clear(map_DeclId_ClassId);
		clear(map_DeclId_Depth);
		declId = 0;
	}
	before node: Declaration -> {
		declId++;		
		push(declarationStack, string(declId));
		map_DeclId_Depth[string(declId)] = string(0);
		
		tempStack : stack of string;
		while(len(declarationStack) > 0) {
			id := pop(declarationStack);
			depth := int(map_DeclId_Depth[id]);
			if (len(tempStack) > depth)
				map_DeclId_Depth[id] = string(len(tempStack));
			push(tempStack, id);	
		}
		while (len(tempStack) > 0)
			push(declarationStack, pop(tempStack));
		
		if (node.kind == TypeKind.CLASS) {
			classIndex++;
			map_DeclId_ClassId[string(declId)] = format("%05d",classIndex);
		}	
	}
	after node: Declaration -> {
		pop(declarationStack);
	}
});
